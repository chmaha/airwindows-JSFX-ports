Version: 1.1
desc:Wide Alchemy (chmaha)
author: chmaha
about:
  Uses DSP from airwindows "SweetWide" by Chris Johnson
  https://github.com/airwindows/airwindows/tree/master/plugins/LinuxVST/src/SweetWide (MIT License)
  JSFX version released under GPLv3 license

slider1:0<0,1,0.01>Soar
slider2:0.5<0,1,0.01>Un/Wide

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init

@slider

A = slider1;
B = slider2;

@block

soar = 0.3-(A*0.3);
blend = -(B-0.5);

@sample
inputSampleL = spl0;
inputSampleR = spl1;

outL = 0.0;
outR = 0.0;

inL = abs(inputSampleL)+(soar*soar);
inR = abs(inputSampleR)+(soar*soar);
(inputSampleL > 0.0) ? outL = max((sqrt(inR/inL)*inL)-soar,0.0);
(inputSampleL < 0.0) ? outL = min((-sqrt(inR/inL)*inL)+soar,0.0);
(inputSampleR > 0.0) ? outR = max((sqrt(inL/inR)*inR)-soar,0.0);
(inputSampleR < 0.0) ? outR = min((-sqrt(inL/inR)*inR)+soar,0.0);

inputSampleL = (outL * blend) + (inputSampleL * (1.0-blend));
inputSampleR = (outR * blend) + (inputSampleR * (1.0-blend));

spl0 = inputSampleL;
spl1 = inputSampleR;

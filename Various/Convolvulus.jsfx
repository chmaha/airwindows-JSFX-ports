Version: 1.0
desc:Convolvulus (chmaha)
author: chmaha
about:
  Uses DSP from airwindows "LRConvolve2" by Chris Johnson
  https://github.com/airwindows/airwindows/tree/master/plugins/LinuxVST/src/LRConvolve2 (MIT License)
  JSFX version released under GPLv3 license

slider1:0<0,1,0.01>Smooth
slider2:0<0,1,1{left,right}>Channel

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init

@slider

A = slider1;
B = slider2;

@block
overallscale = 1.0;
overallscale /= 44100.0;
overallscale *= srate;

smooth = pow(A,4)*(0.5/overallscale);
channel = B;

@sample
inputSampleL = spl0;
inputSampleR = spl1;

carrier = inputSampleL;
modulate = abs(inputSampleR);
(channel > 0.5) ? (
    carrier = inputSampleR;
    modulate = abs(inputSampleL);
);
(iirSample < modulate) ? iirSample = modulate;
modulate = (iirSample*smooth)+(modulate*(1.0-smooth));
(carrier > 0.0) ? carrier = sqrt(carrier*modulate);
(carrier < 0.0) ? carrier = -sqrt(-carrier*modulate);
inputSampleL = inputSampleR = carrier;

spl0 = inputSampleL;
spl1 = inputSampleR;

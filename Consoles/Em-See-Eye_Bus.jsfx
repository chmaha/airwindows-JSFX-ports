NoIndex: true
desc:Em-See-Eye Console Bus (chmaha)
author: chmaha
about:
  Uses DSP from airwindows "ConsoleMDBuss" by Chris Johnson
  https://github.com/airwindows/airwindows/tree/master/plugins/LinuxVST/src/ConsoleMDBuss (MIT License)
  JSFX version released under GPLv3 license

slider1:1<0,1,0.01>Master

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init

gainA = gainB = 1.0;

@slider

A = slider1;

@block
overallscale = 1.0;
overallscale /= 44100.0;
overallscale *= srate;

threshSinew = 0.5171104/overallscale;

@sample
inputSampleL = spl0;
inputSampleR = spl1;

gain = A;
    
(gain < 1.0) ? (
  inputSampleL *= gain;
  inputSampleR *= gain;
);

(inputSampleL > 1.0) ? inputSampleL = 1.0;
(inputSampleL < -1.0) ? inputSampleL = -1.0;
inputSampleL = ((asin(inputSampleL*abs(inputSampleL))/((abs(inputSampleL) == 0.0) ? 1 
: abs(inputSampleL)))*0.618033988749894848204586)+(asin(inputSampleL)*0.381966011250105);

(inputSampleR > 1.0) ? inputSampleR = 1.0;
(inputSampleR < -1.0) ? inputSampleR = -1.0;
inputSampleR = ((asin(inputSampleR*abs(inputSampleR))/((abs(inputSampleR) == 0.0) ? 1
: abs(inputSampleR)))*0.618033988749894848204586)+(asin(inputSampleR)*0.381966011250105);

(gain < 1.0) ? (
  inputSampleL *= gain;
  inputSampleR *= gain;
);

temp = inputSampleL;
clamp = inputSampleL - lastSinewL;
(lastSinewL > 1.0) ? lastSinewL = 1.0;
(lastSinewL < -1.0) ? lastSinewL = -1.0;
sinew = threshSinew * cos(lastSinewL);
(clamp > sinew) ? temp = lastSinewL + sinew;
(-clamp > sinew) ? temp = lastSinewL - sinew;
inputSampleL = lastSinewL = temp;
temp = inputSampleR;
clamp = inputSampleR - lastSinewR;
(lastSinewR > 1.0) ? lastSinewR = 1.0;
(lastSinewR < -1.0) ? lastSinewR = -1.0;
sinew = threshSinew * cos(lastSinewR);
(clamp > sinew) ? temp = lastSinewR + sinew;
(-clamp > sinew) ? temp = lastSinewR - sinew;
inputSampleR = lastSinewR = temp;

(gain < 1.0) ? (
  inputSampleL *= gain;
  inputSampleR *= gain;
);

spl0 = inputSampleL;
spl1 = inputSampleR;
